pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id("io.micronaut.build.shared.settings") version "5.3.14"
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }
    versionCatalogs {
        "mn" {
            from("io.micronaut:micronaut-bom:${providers.gradleProperty("micronautVersion").get()}")
        }
    }
}

rootProject.name = 'graphql-parent'

include "graphql"
include "graphql-bom"

include 'docs-examples:hello-world-java'
include 'docs-examples:hello-world-groovy'
include 'docs-examples:hello-world-kotlin'

[
        "chat",
        "jwt-security",
        "todo-java-tools",
        "todo-spqr"
].each { name ->
    include name
    def project = findProject(":${name}")
    project.name = "graphql-example-${name}"
    project.projectDir = new File(settingsDir, "examples/${name}")
}
